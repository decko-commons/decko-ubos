developer=http://grasscommons.org/
url=https://decko.org/
maintainer=${developer}
pkgname=decko
pkgver=0.6.2
pkgrel=1
pkgdesc="A wiki approach to stuctured data and web design"
arch=('any')
license=("GPL")
source=("https://github.com/decko-commons/decko/archive/v${pkgver}.zip")
depends=(
    'ruby'
    'ruby-bundler'
    'mod_passenger'
    'mariadb-clients'
    'imagemagick'
    'nodejs'
    'memcached'
    'mod_xsendfile'
    'smtp-server'
)

options=('!strip')
sha512sums=('b1cea895f65af4d836c4c291d9811d5c098ab1c041d3022d03baf6e796a8d85ad30f7f65900eeef3ad018b95e63f3b57dfdc72497ba60624178364d8403d67a3')

build() {
    cd ${startdir}
    bundle install --path gems --without development test
}

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Data directory
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Code -- be selective in what we package
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/
    toCopy=(
        '.bundle'
        'Gemfile'
        'Gemfile.lock'
        'Rakefile'
        'config.ru'
        'config/boot.rb'
        'config/environment.rb'
        'config/routes.rb'
        'public/files'
        'script'
        'gems'
        'ubos_cards.pl'
        'ubos_logging.pl'
        'tmpl'
    )
    for t in ${toCopy[@]}; do
        from="${startdir}/${t}"
        if [[ -e ${from} ]]; then
            to="${pkgdir}/ubos/share/${pkgname}/${t}"
            todir=$(dirname $to)
            [[ -d ${todir} ]] || mkdir -p -m 755 ${todir}
            cp -a ${from} ${todir}/
        fi
    done

# Logging
    mkdir -p -m755 ${pkgdir}/var/log/${pkgname}

# License
    install -D -m644 ${startdir}/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
