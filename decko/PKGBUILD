developer=http://grasscommons.org/
url=https://decko.org/
maintainer=${developer}
pkgname=decko
pkgver=0.11.6
pkgrel=2
pkgdesc="A wiki approach to structured data and web design"
arch=('any')
license=("GPL")
source=("https://github.com/decko-commons/decko/archive/v${pkgver}.zip")
depends=(
  'ruby2.7'
  'mod_passenger27'
  'mariadb-clients'
  'imagemagick'
  'nodejs'
  'memcached'
  'mod_xsendfile'
  'smtp-server'
)
options=('!strip')
sha512sums=('51eaf4f184177d34c04f14d21b28a266eb620e76f2487bb3389a8081748a22dd81cb9b11564b956abc4d654569c6cab848e67ff8e1c84e11a0eea88edffd7c58')

prepare() {
  cd ${srcdir}/decko-${pkgver}
  patch -p0 < ${startdir}/rake_command_run_bundle-27.patch
}

build() {
  cd ${srcdir}/decko-${pkgver}
  cp -a ${startdir}/{.bundle,Gemfile,Gemfile.lock} .
  HOME=$(pwd) bundle-2.7 install;
  HOME=$(pwd) bundle-2.7 clean;

  # Remove log files
  logs=$(find . -name gem_make.out -or -name mkmf.log)
  if [[ $logs ]]; then
    rm $logs
  fi

  # Strip binaries
  libs=$(find . -name '*.so' -or -name '*.o')
  if [[ $libs ]]; then
    strip $libs
  fi
}

package() {
# Manifest
  install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
  install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Data directory
  mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Code -- be selective in what we package
  mkdir -p ${pkgdir}/ubos/share/${pkgname}/

# From $startdir
  toCopy=(
    'ubos_cards.pl'
    'ubos_logging.pl'
    'tmpl'
  )
  for t in ${toCopy[@]}; do
    from="${startdir}/${t}"
    to="${pkgdir}/ubos/share/${pkgname}/${t}"
    todir=$(dirname $to)
    [[ -d ${todir} ]] || mkdir -p -m 755 ${todir}
    cp -a ${from} ${todir}/
  done

# From $srcdir:
  toCopy=(
    'gems'
    'Gemfile'
    'Gemfile.lock'
  )

  for t in ${toCopy[@]}; do
    from="${srcdir}/decko-${pkgver}/${t}"
    to="${pkgdir}/ubos/share/${pkgname}/${t}"
    todir=$(dirname $to)
    [[ -d ${todir} ]] || mkdir -p -m 755 ${todir}
    cp -a ${from} ${todir}/
  done

# Systemd
  install -D -m0644 ${startdir}/systemd/*.service -t ${pkgdir}/usr/lib/systemd/system/

# Logging
  mkdir -p -m755 ${pkgdir}/ubos/log

# License
  install -D -m644 ${startdir}/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
