developer=http://grasscommons.org/
url=http://wagn.org/
maintainer=${developer}
pkgname=decko
pkgver=0.3.11
pkgrel=3
pkgdesc="A wiki approach to stuctured data, dynamic interaction, and web design"
arch=('any')
license=("GPL")
# source=("https://github.com/decko-commons/decko/archive/v${pkgver}.zip")
depends=(
    'ruby'
    'ruby-bundler'
    'mod_passenger'
    'mariadb-clients'
    'imagemagick'
    'nodejs'
    'memcached'
    'mod_xsendfile'
)


#
# 'smtp-server'
#options=('!strip')
#sha512sums=('45e4f8682a5e88ef45d658844c1c051468b89204a9e297d967fbde41cee2befb9cd57f69dd3c282820509050af5e5fff7a7fc83205d89ed93e60feaf8c08b596')

build() {
    cd ${startdir}
    bundle install --path gems --deployment --without development test
}

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/${pkgname}
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/${pkgname}/

# Data directory
    mkdir -p ${pkgdir}/var/lib/${pkgname}

# Code
# Code -- be selective in what we package
    mkdir -p ${pkgdir}/usr/share/${pkgname}/
    toCopy=(
        '.bundle'
        'Gemfile'
        'Gemfile.lock'
        'Rakefile'
        'config.ru'
        'config/boot.rb'
        'config/environment.rb'
        'config/routes.rb'
        'public/files'
	'script'
        'gems'
        'ubos_cards.pl'
        'ubos_logging.pl'
        'tmpl'
    )
    for t in ${toCopy[@]}; do
        from="${startdir}/${t}"
        if [[ -e ${from} ]]; then
            to="${pkgdir}/usr/share/${pkgname}/${t}"
            todir=$(dirname $to)
            [[ -d ${todir} ]] || mkdir -p -m 755 ${todir}
            cp -a ${from} ${todir}/
        fi
    done

# Logging
    mkdir -p -m755 ${pkgdir}/var/log/${pkgname}

# License
    mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
    install -m644 ${startdir}/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
