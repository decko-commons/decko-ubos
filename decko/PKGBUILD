developer=http://grasscommons.org/
url=https://decko.org/
maintainer=${developer}
pkgname=decko
pkgver=0.8.3
pkgrel=5
pkgdesc="A wiki approach to stuctured data and web design"
arch=('any')
license=("GPL")
source=("https://github.com/decko-commons/decko/archive/v${pkgver}.zip")
depends=(
    'ruby'
    'ruby-bundler'
    'mod_passenger'
    'mariadb-clients'
    'imagemagick'
    'nodejs'
    'memcached'
    'mod_xsendfile'
    'smtp-server'
)

options=('!strip')
sha512sums=('1870978163e736d935d6bdacfb0b30f24814b69007108555a185161e416292f9bb00dbbbf6735ec2e8b0731ccbd497e73d6f8a2f0f3e3f0ad940fde41dac986c')

build() {
    cd ${srcdir}/decko-${pkgver}
    cp -a ${startdir}/{config,public,script,config.ru,Gemfile,Gemfile.lock,Rakefile} .
    # HOME=$(pwd) bundle install --path vendor/bundle
    HOME=$(pwd) bundle config set path 'vendor/bundle'
    HOME=$(pwd) bundle update --bundler
    HOME=$(pwd) bundle install --path gems --without development test

    # Remove log files
    logs=$(find . -name gem_make.out -or -name mkmf.log)
    if [[ $logs ]]; then
        rm $logs
    fi

    # Strip binaries
    libs=$(find . -name '*.so' -or -name '*.o')
    if [[ $libs ]]; then
        strip $libs
    fi
}

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Data directory
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Code -- be selective in what we package
    mkdir -p ${pkgdir}/ubos/share/${pkgname}/

# From $startdir
    toCopy=(
        'ubos_cards.pl'
        'ubos_logging.pl'
        'tmpl'
    )
    for t in ${toCopy[@]}; do
        from="${startdir}/${t}"
        to="${pkgdir}/ubos/share/${pkgname}/${t}"
        todir=$(dirname $to)
        [[ -d ${todir} ]] || mkdir -p -m 755 ${todir}
        cp -a ${from} ${todir}/
    done

# From $srcdir:
    toCopy=(
        '.bundle'
        'Gemfile'
        'Gemfile.lock'
        'Rakefile'
        'config.ru'
        'config/boot.rb'
        'config/environment.rb'
        'config/routes.rb'
        'public/files'
        'gems'
        'script'
    )

    for t in ${toCopy[@]}; do
        from="${srcdir}/decko-${pkgver}/${t}"
        to="${pkgdir}/ubos/share/${pkgname}/${t}"
        todir=$(dirname $to)
        [[ -d ${todir} ]] || mkdir -p -m 755 ${todir}
        cp -a ${from} ${todir}/
    done

# Systemd
    install -D -m0644 ${startdir}/systemd/*.service -t ${pkgdir}/usr/lib/systemd/system/

# Logging
    mkdir -p -m755 ${pkgdir}/ubos/log

# License
    install -D -m644 ${startdir}/LICENSE -t ${pkgdir}/usr/share/licenses/${pkgname}/

# Fix shared library temporarily
    (cd ${pkgdir}/ubos/share/decko/gems/ruby/2.7.0/gems/sassc-2.2.0/lib/sassc && ln -s ../../ext/libsass.so)
}
